apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
// This does not break the build when Android Studio is missing the JRebel for Android plugin.
apply plugin: 'com.zeroturnaround.jrebel.android'

def getSecretProperty(name) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('secret.properties').newDataInputStream())

    return properties.getProperty(name.toString())
}

def TWITTER_API_KEY = '"' + getSecretProperty("TwitterApiKey") + '"'
def TWITTER_API_SECRET = '"' + getSecretProperty("TwitterApiSecret") + '"'

def FABRIC_API_KEY = getSecretProperty('FabricApiKey')

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    defaultConfig {
        applicationId "net.yslibrary.photosearcher"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField 'String', 'TWITTER_API_KEY', TWITTER_API_KEY
        buildConfigField 'String', 'TWITTER_API_SECRET', TWITTER_API_SECRET

        manifestPlaceholders = [fabricApiKey: FABRIC_API_KEY]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'

    compile 'org.apache.commons:commons-lang3:3.3.2'

    compile 'com.squareup.okhttp:okhttp:2.4.0'

    compile 'com.squareup:otto:1.3.8'

    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.1'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:3.1.0'

    compile 'io.reactivex:rxandroid:1.0.1'

    compile 'io.realm:realm-android:0.82.1'

    compile 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:1.0.0'

    // dagger
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'net.danlew:android.joda:2.8.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true;
    }
    compile('com.twitter.sdk.android:twitter:1.7.2@aar') {
        transitive = true;
    }

    // LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    // Debug bridge
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.1.1'
    debugCompile 'com.facebook.stetho:stetho-timber:1.1.1'
}
